plugins {
    id "net.neoforged.moddev.legacyforge"
    id "maven-publish"
}

version = findProperty('mod_version') ?: mod_version
group = mod_group
archivesBaseName = mod_id

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev/"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "Curios"
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name = "SpongePowered"
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        name = "CurseMaven"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    // AE2 from Modrinth
    implementation "maven.modrinth:ae2:7KVs6HMQ"

    // Curios API
    compileOnly "top.theillusivec4.curios:curios-forge:5.4.7+1.20.1:api"
    runtimeOnly "top.theillusivec4.curios:curios-forge:5.4.7+1.20.1"

    // FancyMenu (optional compat)
    compileOnly "maven.modrinth:fancymenu:MywA2BxT"
    compileOnly "maven.modrinth:konkrete:skYziQQL"

    // Mixin
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    compileOnly 'org.jetbrains:annotations:24.1.0'
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

    runs {
        configureEach {
            gameDirectory = project.file('run')
        }
        client {
            client()
        }
        server {
            server()
        }
    }
}

processResources {
    var expansions = [
        "mod_id": mod_id,
        "mod_name": mod_name,
        "mod_version": mod_version,
        "minecraft_version_range": minecraft_version_range,
        "forge_version_range": forge_version_range
    ]

    inputs.properties(expansions)

    filesMatching("META-INF/mods.toml") {
        expand expansions
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes([
            'MixinConfigs': 'personalmesystem.mixins.json'
        ])
    }
}
